const ion = import '../'

export class DynamicExpression extends import './Expression'
    properties:
        isActive: false
        activate: ->
            # called when we have watchers
            @isActive = true
        deactivate: ->
            # called when we no longer have watchers
            @isActive = false
        watchValue: (watcher) ->
            let watchers = @_watchers ?= []
            if watchers.length is 0
                @activate()
            watchers.push(watcher)
            # notify watcher immediately if we have a value
            if @hasValue()
                let value = @getValue()
                @_notifyWatcher(watcher, value)
        unwatchValue: (watcher) ->
            @_watchers.remove(watcher)
            # notify watcher immediately if we have a value
            if @hasValue()
                @_notifyWatcher(watcher, undefined)
            # this must happen AFTER we notify the watcher,
            # otherwise it changes the value and the watcher may not be notified.
            if @_watchers.length is 0
                @deactivate()
        _notifyWatcher: (watcher, value) -> watcher.call(@, value)
        notify: ->
            if @_watchers?
                let value = @getValue()
                for watcher in @_watchers
                    @_notifyWatcher(watcher, value)
            return
        hasValue: -> @hasOwnProperty('value')
        getValue: -> @value
        setValue: (value) ->
            if value isnt @value or not @hasValue()
                @value = value
                @notify()
            return
    test: ->
        const d = new DynamicExpression()
        if d.getValue() isnt undefined
            throw "d.getValue() != undefined"
        let total = 10
        const watcher = (value) ->
            if value isnt undefined
                total += value
        d.watchValue(watcher)
        assert total is 10
        d.setValue(10)
        assert d.getValue() is 10
        assert total is 20
        d.setValue(20)
        assert total is 40
        d.unwatchValue(watcher)
        assert total is 40
        d.setValue(50)
        assert total is 40
