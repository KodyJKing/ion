const
    ion = import '../'
    Statement = import './Statement'

export class Property extends Statement
    properties:
        activate: ->
            super
            # create the runtime expressions
            @keyExpression ?= @context.createRuntime(@computed ? @key : @key.name)
            @valueExpression ?= @context.createRuntime(@value)
            # then watch them, this way, the keywatcher can set the left value on the right hand side
            # before it is activated
            (@keyExpression.watch)
                @keyWatcher ?= (key) =>
                    if key? and @valueExpression.setLeftValue?
                        let currentValue = ion.get(@context.output, key)
                        if currentValue?
                            @valueExpression.setLeftValue(currentValue)

                    @restoreProperty()
                    @keyValue = key
                    @setProperty()
            (@valueExpression.watch)
                @valueWatcher ?= (value) =>
                    @valueValue = value
                    @setProperty()
        deactivate: ->
            super
            @restoreProperty()
            @keyExpression.unwatch(@keyWatcher)
            @valueExpression.unwatch(@valueWatcher)
        restoreProperty: ->
            if @originalKey?
                ion.set(@context.output, @originalKey, @originalValue)
                @originalKey = undefined
                @originalValue = undefined
        setProperty: (key = @keyValue, value = @valueValue) ->
            if key? and value isnt undefined
                let currentValue = ion.get(@context.output, key)
                if currentValue isnt value
                    @originalKey ?= key
                    @originalValue ?= currentValue
                    ion.set(@context.output, key, value)
